plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

archivesBaseName = "game-support-server"
mainClassName = "org.triplea.server.GameSupportServerApplication"
ext {
    releasesDir = file("$buildDir/releases")
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
}

task portableInstaller(type: Zip, group: "release", dependsOn: shadowJar) {
    from file("configuration.yml")

    from(shadowJar.outputs) {
        into "bin"
    }
}

task release(group: "release", dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

shadowJar {
    archiveClassifier.set ""
    // mergeServiceFiles is needed by dropwizard
    // Without this configuration parsing breaks and is unable to find connector type "http" for
    // the following YAML snippet:  server: {applicationConnectors: [{type: http, port: 8080}]
    mergeServiceFiles()
}

configurations {
    testImplementation {
        // database-rider brings in slf4j-simple as a transitive dependency
        // DropWizard has logback baked in and cannot have multiple slf4j bindings.
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
}


dependencies {
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation project(":http-clients:github-client")
//    implementation project(":lib:http-client-lib")
//    implementation project(":servers:game-support:client")
    implementation project(":server:server-lib")

    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"

    testImplementation "com.github.database-rider:rider-junit5:$databaseRiderVersion"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
//    testImplementation project(":lib:test-common")
    testImplementation project(":server:database-test-support")
}
